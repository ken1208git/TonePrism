## データベース管理クラス
## SQLiteデータベースの接続、バージョン管理、ユーティリティを提供
## ゲームデータのクエリは GameRepository / StoreSectionRepository を使用

extends RefCounted
class_name DatabaseManager

var db: SQLite = null
var db_path: String = ""

# Launcher が想定する DB スキーマバージョン
# Manager 側 SchemaManager.cs の CurrentDbVersion と歩調を合わせること
# (v9, v10, v12 は backup_log 関連 / v13 は manager_sessions で Launcher は触らない /
#  v11 の surveys・play_records 新スキーマには Launcher のクエリが既に対応済 /
#  v14 は games.arguments の正規 migration 化のみで最終スキーマ不変・Launcher は arguments 対応済
#  v15/v17 は game_versions の UNIQUE INDEX 強化 (NOCASE 化含む)、v16 は backup_log CHECK 拡張、
#  v19 は backup_log DROP → いずれも Launcher が読まないテーブル / index のみで読み取り不変 /
#  v18 は developers.version_id FK + ON DELETE CASCADE 追加 / game_genres dead table 除去・
#  v20 は games.play_time に CHECK(1-3) 追加 → 制約追加のみで Launcher の読み取りクエリは不変 /
#  v21/v22 は intro_slides テーブル新設と duration_sec 削除 (#253)。Launcher は本テーブルを
#  IntroSlideRepository (手動ナビの初回説明) で読むため、ここで明示的に対応版数を v22 へ追従させる
#  → v15〜v22 は定数追従 (intro_slides は本リリースで実クエリ対応済))
const CURRENT_DB_VERSION: int = 22

## データベースを開く
func open() -> bool:
	if db != null:
		return true

	db_path = PathManager.get_database_path()

	# データベースファイルが存在するか確認
	if not FileAccess.file_exists(db_path):
		push_error("[DatabaseManager] データベースファイルが見つかりません: " + db_path)
		return false

	# SQLiteインスタンスを作成
	db = SQLite.new()

	# データベースパスを設定
	db.path = db_path

	# データベースを開く（godot-sqliteのAPI）。失敗時 (パス不正・権限不足・ロック等) は
	# false が返る。db オブジェクト自体は非 null のままなので、戻り値で必ず判定する。
	if not db.open_db():
		push_error("[DatabaseManager] データベースを開けませんでした: " + db_path)
		db = null
		return false

	print("[DatabaseManager] データベースを開きました: ", db_path)

	# SMB ネットワーク共有上での運用安全性のため DELETE モードを使用 (#103)
	# busy_timeout で書き込み競合時の待機列を確保（10000ms）
	db.query("PRAGMA journal_mode=DELETE;")
	db.query("PRAGMA busy_timeout=10000;")

	# データベースのバージョンチェックとマイグレーション
	_check_and_migrate_db()

	return true

## データベースを閉じる
func close() -> void:
	if db != null:
		db.close_db()
		db = null
		print("[DatabaseManager] データベースを閉じました")

## データベースが開いているか確認
func is_open() -> bool:
	return db != null

## データベースのバージョンを取得
func _get_db_version() -> int:
	if db == null:
		return 0

	# PRAGMA user_version を取得
	db.query("PRAGMA user_version")
	var result = db.get_query_result()

	if result != null and result.size() > 0:
		# 結果の形式に合わせて取得（辞書または配列）
		var row = result[0]
		if row is Dictionary:
			var ver = row.get("user_version", 0)
			return int(ver) if ver != null else 0
		elif row is Array and row.size() > 0:
			var ver = row[0]
			return int(ver) if ver != null else 0

	return 0

## データベースのバージョンを設定
func _set_db_version(version: int) -> void:
	if db == null:
		return

	db.query("PRAGMA user_version = %d" % version)
	print("[DatabaseManager] データベースバージョンを %d に更新しました" % version)

## データベースのバージョンチェックとマイグレーションを実行
func _check_and_migrate_db() -> void:
	var current_version = _get_db_version()
	print("[DatabaseManager] 現在のDBバージョン: %d, Launcher期待バージョン: %d" % [current_version, CURRENT_DB_VERSION])

	# バージョンが0の場合（新規作成時など）、本来はManagerで初期化すべきだが、
	# 万が一の場合はManagerに任せる方針とする（Launcherは基本Read-Onlyに近い）
	if current_version == 0:
		print("[DatabaseManager] Warning: DBバージョンが0です。Managerによる初期化が必要な可能性があります。")
		return

	# バージョンが新しい場合（将来のバージョン）
	if current_version > CURRENT_DB_VERSION:
		push_warning("[DatabaseManager] DBバージョン(%d)がLauncherの対応バージョン(%d)より新しいです。一部機能が正常に動作しない可能性があります。" % [current_version, CURRENT_DB_VERSION])
		return

	# マイグレーションが必要な場合（古いバージョン）
	if current_version < CURRENT_DB_VERSION:
		push_warning("[DatabaseManager] Warning: DBバージョン(%d)が古いです。Managerを起動してDBを更新することをお勧めします。" % current_version)

## 値を安全にintに変換する（null対策）
func safe_int(val, default_val: int = 0) -> int:
	if val == null:
		return default_val
	return int(val)

## 値を安全にboolに変換する（null対策）
func safe_bool(val, default_val: bool = false) -> bool:
	if val == null:
		return default_val
	# 数値（0 or 1）として扱われる場合が多いため、int経由で変換
	return bool(int(val))
